#read travel.tin
#read spelltracker.tin
/* #read glance.tin */
#read return.tin
#read search.tin
#read stand.tin

#config           {256 COLORS}  {ON}
#config           {AUTO TAB}  {5000}
#config           {BUFFER SIZE}  {20000}
#config           {CHARSET}  {ASCII}
#config           {COMMAND COLOR}  {<078>}
#config           {COMMAND ECHO}  {ON}
#config           {CONNECT RETRY}  {15}
#config           {HISTORY SIZE}  {1000}
#config           {PACKET PATCH}  {0.00}
#config           {REPEAT CHAR}  {!}
#config           {REPEAT ENTER}  {OFF}
#config           {SCROLL LOCK}  {ON}
#config           {SPEEDWALK}  {ON}
#config           {TINTIN CHAR}  {#}
#config           {VERBATIM}  {OFF}
#config           {VERBATIM CHAR}  {\}
#config           {VERBOSE}  {OFF}
#config           {WORDWRAP}  {ON}


#pathdir          {n}                     {s}                          {1}
#pathdir          {e}                     {w}                          {2}
#pathdir          {ne}                    {sw}                         {3}
#pathdir          {s}                     {n}                          {4}
#pathdir          {se}                    {nw}                         {6}
#pathdir          {w}                     {e}                          {8}
#pathdir          {nw}                    {se}                         {9}
#pathdir          {sw}                    {ne}                         {12}
#pathdir          {u}                     {d}                          {16}
#pathdir          {d}                     {u}                          {32}

#var expmode 0
#var autocast 0
#var asearch 0
#var imafk 0
#var Acircl 0
#var smscontrol 0
#var Aloot 0
#var Adrain 0
#var stonetank 0
#var petitioned 0
#var blurtank 0
#var currtells 0
#var curracc 0
#var currgs 0
#var currpet 0
#var currlrn 0
#var expmode 0
#var docast 0
#var checkcast 0
#var icasted 0
#var healwhenback 0
#var tdoor 1
#var blurwhenback 0
#var adie 0
#var dieticker 0
#var dietickeron 0
#var stonewhenback 0
#var icasted 0
#var assist 0
#var pres_1round 0
#var pres_auto 0
#var loaded 0
#var autoassist 0
#var aflee 1
#var Agroup 0
#var gleader 0
#var myname 0
#var auto_path 0

#gag {Your spell is partially absorbed}
#gag {If you do not get a response, it's because the staff are busy or unavailable.}
#gag {If this is a pressing matter, please mudmail admin by typing mwrite admin}
#gag {<subject> and your message.}

#config {command echo} {off}

#unprompt %*
#action {< %1h/%2H %3v/%4V >} {
	#math {myhper} {%1 * 100 / %2};
    #if {$myhper > 100} { #var currhealth {<120>%1};
        #var chgr {<120>||||||||||||||||};};
    #if {$myhper == 100 } { #var currhealth {<020>%1};
        #var chgr {<020>||||||||||||||};};
    #if {$myhper < 100 && $myhper >= 90} { #var currhealth {<030>%1};
        #var chgr {<030>||||||||||||};};
    #if {$myhper < 90 && $myhper >= 75} { #var currhealth {<130>%1};
        #var chgr {<130>||||||||||};};
    #if {$myhper < 75 && $myhper >= 50} { #var currhealth {<150>%1};
        #var chgr {<150>||||||||};};
    #if {$myhper < 50 && $myhper >= 30} { #var currhealth {<050>%1};
        #var chgr {<050>||||||};};
    #if {$myhper < 30 && $myhper >= 15} { #var currhealth {<110>%1};
        #var chgr {<110>||||};};
    #if {$myhper < 15 && $myhper >= 0} { #var currhealth {<010>%1};
        #var chgr {<010>||} };
}

#action {%0 suddenly looks speechless!} {gsay %0 SILENCED}
#action {(Head) %0 - %1} {#var gleader %1}
#action {< P: %1 >} {#var incombat 0;#nop;}
#action {< T: %1 TC:} {#var incombat 1;#nop;}
#action {Autosaving} {save}
#action {PANIC!  You couldn't escape!}  {#if {$aflee == 1} {flee}}
#action {The %1 seems to be closed.}  {#if {$auto_path == 1 || $auto_map == 1} {#map undo};#if {$tdoor == 1} {open %1 $ld}}
#action {Welcome to Toril!} {#tick {scorestart} {score;#untick scorestart;#nop #split 0 3;} {1}}
#action {Reconnecting.} {#tick {scorestart} {score;#untick scorestart;#nop #split 0 3;} {1}}
#action {Disconnecting previous link.} {#tick {scorestart} {score;#untick scorestart;#nop #split 0 3;} {1}}
#action {Please enter your term} {#CR}
#action {^Account name:} {thegnome}


#alias connect {
  #session toril torilmud.com 9999;
  #SPLIT {0} {0};
}

#alias mirror {
  #session mirrormud torilmud.org 6000;
  #SPLIT {0} {0};
}

/*
#action {Score information for %w%s$} {
  #if {$loaded} {} {
    #var {myname} {%1};
    lchar
  };
};
*/


#action {^Level: %1 Race: %2 Class: %w%s$} {
  #var {myclass} {%3};

  #if {$loaded} {} {
		#if {"$myclass" == "Enchanter"} {lench;  #var loaded 1};
		#if {"$myclass" == "Elementalist"} {lelem;  #var loaded 1};
		#if {"$myclass" == "Illusionist"} {lill;   #var loaded 1};
		#if {"$myclass" == "Necromancer"} {lnec;   #var loaded 1};
		#if {"$myclass" == "Warrior"} {lwarr;  #var loaded 1};
		#if {"$myclass" == "Shaman"} {lsham;  #var loaded 1};
		#if {"$myclass" == "Rogue"} {lrogue; #var loaded 1};
        #if {"$myclass" == "Druid"} {ldruid; #var loaded 1};
        #if {"$myclass" == "Psionicist"} {lpsion; #var loaded 1};
	};
};


/* Character Loading */

#alias lrogue {#read rogue.tin}
#alias lench {#read enchanter.tin}
#alias lelem {#read elementalist.tin}
#alias lnec {#read necro.tin}
#alias lill {#read illusionist.tin}
#alias lsham {#read shaman.tin}
#alias lwarr {#read warrior.tin}
#alias ldruid {#read druid.tin}
#alias lpsion {#read psionicist.tin}


#action {Score information for %1} {#var myname %1}

#action {You rejoin the land of the living...} {/* #send assoc chat mung thepasswrd; */}
/* #macro {\t} {#cursor TAB FORWARD} */

#action {%1 is mortally wounded, and will die soon, if not aided.} {
  #if {$Adrain == 1} {drain $drain_target}
}

#action {You receive %1 experience.}  {
  #if {$autoassist == 1} {#var assist 1};
  #if {$Acircl == 1} {#untick circl};
  #if {$docast == 1} {dco};
  #if {$cPrac == 1} {#untick chargePractice;#var cPrac 0};
  #if {$asearch == 1} {srch corpse};
  #if {$Aloot == 1} {loot;p all.coin};
  #if {$Apoison == 1} {ap};
  #if {$dietickeron == 1} {#untick dieticker;#var adie 1};
  #if {$aResc == 1} {rr};
  #var ccph 0;
  #var ccawf 0;
}

#action {Casting: %0 *%1} {
        #if {&{castarg}} {#nop} {#if {$incombat == 1} {#var castarg fighting} {#var castarg room}};
        #UNSUB {Casting: %0 *%1};
        #if {@getlen{%1} > 3} {
                #sub {Casting: %0 *%1} {<070>cast <100>(<060>%0<070> <100>-> <160>$castarg<100>): <100>*%1};
        };
        #if {@getlen{%1} <= 3} {
                #sub {Casting: %0 *%1} {<070>cast <100>(<060>%0<070> <100>-> <160>$castarg<100>): <140>*%1};
        }
}

/* Speedwalker */

#alias {.%0}
{
  #var cnt {};

  #parse {%0} {char}
  {
    #if {"$char" >= "0" && "$char" <= "9"}
    {
      #var cnt $cnt$char
    };
    #elseif {"$cnt" == ""}
    {
      #send $char
    };
    #else
    {
      #$cnt #send $char;
      #var cnt {}
    }
  }
}

/* Couple Toggles */

#alias tloot {
    #if {$Aloot == 1} {
                #var Aloot 0;
                #echo {%c%h} {red} { AUTO LOOTER off }
                } {
                #var Aloot 1;
                #echo {%c%h} {green} { AUTO LOOTER on }
                }
}

#alias tdrain {
    #if {$Adrain == 1} {
                #var Adrain 0;
                #echo {%c%h} {red} { AUTO DRAINER off }
                } {
                #var Adrain 1;
                #echo {%c%h} {green} { AUTO DRAINER on }
                }
}

#alias tsms {
    #if {$smscontrol == 1} {
                #var smscontrol 0;
                #echo {%c%h} {red} { SMS CONTROL off };
				#untick smscontroller;
                } {
                #var smscontrol 1;
                #echo {%c%h} {green} { SMS CONTROL on };
					#tick {smscontroller} {
						#script {smsobject} {cat ~/tt/ttsms/data.txt};
						#if {"$smsobject" != ""} {$smsobject[1]};
						#system {rm ~/tt/ttsms/data.txt};
						#system {touch ~/tt/ttsms/data.txt};
						#system {chmod 777 ~/tt/ttsms/data.txt};
					} {15};
                }
}

#alias tsearch {
        #if {$asearch == 1} {
                #var asearch 0;
                #echo {%c%h} {red} { AUTO SEARCH off }
                } {
                #var asearch 1;
                #echo {%c%h} {green} { AUTO SEARCH on }
        }
}

#alias tdoors {
	#if {$tdoor == 1} {
                #var tdoor 0;
                #echo {%c%h} {red} { AUTO OPEN DOORS off }
                } {
                #var tdoor 1;
                #echo {%c%h} {green} { AUTO OPEN DOORS on }
        }
}


#alias tflee {
    #if {$aflee == 1} {
                #var aflee 0;
                #echo {%c%h} {red} { AUTO FLEE off }
                } {
                #var aflee 1;
                #echo {%c%h} {green} { AUTO FLEE on }
        }
}

#alias tafk {
    #if {$imafk == 1} {
				tog afk;
                #var imafk 0;
                #echo {%c%h} {red} { AFK off };
		#SYSTEM cat afkcomms.txt;
		#system rm afkcomms.txt
                } {
				tog afk;
                #var imafk 1;
                #echo {%c%h} {green} { AFK on };
		#line log afkcomms.txt {<100>...<090>}
        }
}

#alias tgroup {
        #if {$Agroup == 1} {
                #var Agroup 0;
                #echo {%c%h} {red} { AUTO GROUP off }
                } {
                #var Agroup 1;
                #echo {%c%h} {green} { AUTO GROUP on }
                }
}

#alias tcond {
        #if {$condcall == 1} {
                #var condcall 0;
                #echo {%c%h} {red} { COND CALLER off }
                } {
                #var condcall 1;
                #echo {%c%h} {green} { COND CALLER on }
                }
}

#alias {tpoison} {
        #if {$auto_poison == 1} {
                #var auto_poison 0;
                #echo {%c%h} {red} { AUTO POISON off }
                } {
                #var auto_poison 1;
                #echo {%c%h} {green} { AUTO POISON on }
                }
}

#alias {tbash} {
	#if {$autobash == 1} {
		#var autobash 0;
		#echo {%c%h} {red} { AUTOBASH off }
		} {
		#var autobash 1;
		#echo {%c%h} {green} { AUTOBASH on }
		}
}

#action {%0 has just given you %1 consent.} {
	#if {$Agroup == 1} {
		#var thetime {@gettime{}};
		group %0;
		gsay -- grouped %0 at $thetime EST --;
		#if {"$myname" ==  "Eln"} {radd %0};
	}
}


/* Global Macros */

#macro {\C-f} {flee}
#macro {\C-l} {get all corpse}
#macro {\C-p} {#buffer lock}

#macro {\eOp} {scan}
#macro {\eOr} {s;#var ld s}
#macro {\eOs} {d;#var ld d}
#macro {\eOw} {d;#var ld d}
#macro {\eOt} {w;#var ld w}
#macro {\eOu} {look}
#macro {\eOv} {e;#var ld e}
#macro {\eOx} {n;#var ld n}
#macro {\eOy} {u;#var ld u}
#macro {\eOq} {#send {look \}}
#macro {\eOl} {#send l}

#macro {\e[23^} {scan}

#alias s {#send s;#var ld s}
#alias w {#send w;#var ld w}
#alias n {#send n;#var ld n}
#alias e {#send e;#var ld e}
#alias u {#send u;#var ld u}
#alias d {#send d;#var ld d}

/* Global Aliases */

#alias {kpfix} {#showme {\e>}}
#alias {stat} {tell katumi stat %0}
#alias {find} {tell nerun find %1}
#alias {whois} {tell nerun who %1}
#alias {inv} {#send inventory}
#alias {'} {gsay %0}
#alias {`} {#var enemy %1;con $enemy}
#alias {aa} {assist $tank}
#alias {f} {forget %0}
#alias {clr} {#var currtells 0; #var curracc 0; #var currgs 0; #var currlrn 0;}
#alias {sskin} {innate scale}
#alias {wi} {wiz &cL%0&N}
#alias {vm} {innate viper}
#alias {pri} {prioritize %0}
#alias {ffl} {say freezing flesh}
#alias {def} {say defender of asgard}
#alias {ber} {say berserker fury}
#alias {pm} {say Protect me}
#alias {fm} {say flesh mends}
#alias {rel} {say Release}
#alias {glb} {say deeply defend}
#alias {hst} {quaff haste $bag}
#alias {amp} {quaff ampoule $bag}
#alias {vit} {quaff vit $bag}
#alias {ds} {say fell stars}
#alias {d1} {say Dragonpoison}
#alias {d2} {say Dragonslow}
#alias {d3} {say Dragonblind}
#alias {d4} {say Dragonstrike}
#alias {bb} {b1;b2;b3;b4}
#alias {pp} {put %1 $pbag}
#alias {p} {put %1 $bag}
#alias {p2} {put %1 $bag2}
#alias {gp} {get %1 $pbag}
#alias {g} {get %1 $bag}
#alias {g2} {get %1 $bag2}
#alias {ggs} {#var currgs 0;#grep group-says}
#alias {ggcc} {#grep GCC:}
#alias {gacc} {#var curracc 0;#grep ASSOC:}
#alias {glrn} {#var currlrn 0;#grep learned something}
#alias {gpet} {#var currpet 0;#grep petition}
#alias {gt} {#var currtells 0;#grep tells you}
#alias {ke} {#var enemy %1;kill $enemy}
#alias {lib} {look in $bag}
#alias {lib2} {look in $bag2}
#alias {tank} {#var tank @upfirst{%1};#echo {%c%h} {blue} { TANK is $tank }}
#alias {w1} {#var w1 %1}
#alias {w2} {#var w2 %1}
#alias {wes} {who evil sort}
#alias {wgs} {who good sort}
#alias {ws} {who sort}
#alias {wig} {who ingroup s}
#alias {wns} {who neutral sort}
#alias {srm} {rest;mem}
#alias {srp} {rest;pray}

#alias {imafk} {
	#var wentafk {@gettime{}};
	#if {$imafk} {#var afkmessage {%0 - Away since $wentafk EST}} {#var afkmessage {%0 - Away since $wentafk EST};tafk;}
}
#alias {imback} {tafk}

#alias {lic} {#if {"%1" == ""} {#var licl 1} {#var licl %1};#loop {1} {$licl} {cnt} {exa $cnt.corpse}}
#alias {lip} {#if {"%1" == ""} {#var lipl 1} {#var lipl %1};#loop {1} {$lipl} {cnt} {l in $cnt.portal}}
#alias {lipc} {#if {"%1" == ""} {#var lipcl 1} {#var lipcl %1};#loop {1} {$lipcl} {cnt} {exa $cnt.pcorpse}}
#alias {loot} {#if {"%1" == ""} {#var lootl 1} {#var lootl %1};#loop {1} {$lootl} {cnt} {get all $cnt.corpse}}

#alias po {
	#if {"%1" == "w"} {emote points to the room to the west.};
	#if {"%1" == "e"} {emote points to the room to the east.};
	#if {"%1" == "s"} {emote points to the room to the south.};
	#if {"%1" == "n"} {emote points to the room to the north.};
	#if {"%1" == "u"} {emote points to the room above.};
	#if {"%1" == "d"} {emote points to the room below.};
}

#alias gl {glance %1}

#alias {of}
{
    #send {order followers \};
    %0
}

#alias {o %* %*}
{
    #send {order %1 \};
    %2
}

#alias {ofce} {of %1 $enemy}
#alias {off} {of flee}
#alias {ofbe} {of bash $enemy}
#alias {ofb} {of bash}
#alias {ofke} {of kill $enemy}
#alias {rm} {of rescue $myname}
#alias {r} {of rescue %1}
#alias {rn} {fols name %1 %2}

/* Global Functions */

#function {gettime} {#FORMAT {time} {%t} {%r};#FORMAT {test} {%t} {$time};#var result {$test}}
#function {lcase} {#format {result} {%l} {%0}}
#function {upfirst} {#format {result} {%n} {%1}}
#function {getlen} {#format {result} {%L} {%1}}
#function {isvar} {#if {"$%0" == "?%0"} {#var result 0} {#var result 1}}
#function {rightalign} {
  #format {colwidth} {%C};
  #format {stringlength} {%L} {{%0}};
  #math   {padding} {$colwidth - $stringlength};
  #format {result} {%+${padding}s%s} {{} {%0}}
}

#sub {~You %1single hit.} {You %1<100>single hit<098>.}
#sub {~You %1two hits.} {You %1<030>two hits<098>.}
#sub {~You %1three hits.} {You %1<010>three hits<098>.}
#sub {~You %1four hits.} {You %1<110>four hits<098>.}
#sub {~You %1five hits.} {You %1<130>five hits<098>.}
#sub {~You %1six hits.} {You %1<120>six hits<098>.}
#sub {~You %1seven hits.} {You %1<150>seven hits<098>.}
#sub {~You %1eight hits.} {You %1<180>eight hits<098>.}

#action {%w group-%3s '%0'$} {
   #if {"$stgsay" == "1"} {
	#if {"@lcase{%0}" == "%*haste%*"}
		{
		#var thetime {@gettime{}};
		delaffect %1 haste;sg;
   	    #line log comms.txt {<100>$thetime <090>%1 : <110>%0<090>}
		};
	#elseif {"@lcase{%0}" == "%*$myname%*}
		{
		#var thetime {@gettime{}};
		#line log comms.txt {<100>$thetime <090>%1 : <150>%0<090>}
		}
	#elseif {"@lcase{%0}" == "%*blur%*"}
		{
		#var thetime {@gettime{}};
		delaffect %1 blur;sg;
        #line log comms.txt {<100>$thetime <090>%1 : <140>%0<090>}
		};
	#elseif {"@lcase{%0}" == "%*vit%*"}
        {
        #var thetime {@gettime{}};
        delaffect %1 vit;sg;
        #line log comms.txt {<100>$thetime <090>%1 : <040>%0<090>}
        };
	#elseif {"@lcase{%0}" == "%*globe%*"}
        {
        #var thetime {@gettime{}};
        delaffect %1 globe;sg;
        #line log comms.txt {<100>$thetime <090>%1 : <010>%0<090>}
        };
	#elseif {"@lcase{%0}" == "%*stone%*"}
		{
		#var thetime {@gettime{}};
		delaffect %1 stone;sg;
   	    #line log comms.txt {<100>$thetime <090>%1 : <100>%0<090>}
		};
	#elseif {"@lcase{%0}" == "%*scale%*" || "@lcase{%0}" == "%*dscale%*"}
		{
		#var thetime {@gettime{}};
		delaffect %1 scale;sg;
    	#line log comms.txt {<100>$thetime <090>%1 : <130>%0<090>}
		};
	#elseif {"@lcase{%0}" == "%*displace%*"}
		{
		#var thetime {@gettime{}};
    	delaffect %1 disp;sg;
    	#line log comms.txt {<100>$thetime <090>%1 : <050>%0<090>}
    	};
	#ELSE {
		#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>%1 : <030>%0<090>}
	}
   } {#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>%1 : <030>%0<090>}};
	#math currgs {$currgs + 1};
}

#action {%1 group-say '%1'$} {
  #var thetime {@gettime{}};
  #line log comms.txt {<100>$thetime <120>-\> <090>: <030>%1<090>}
}

#action {%1 tells you '%2'$} {
	#if {$imafk == 0} {
		#if {"%1" == "nerun"} {
			#var thetime {@gettime{}};#line log comms.txt {<100>$thetime nerun <090>: <060>%2<090>};
			#if {$smscontrol == 1} {#system dosms -p 15617040453 -t "%2";};
		} {
			#if {"%1" != "%* %*"} {
				#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>%1 <100>: <170>%2<090>}
			};
			#if {$smscontrol == 1} {#system dosms -p 15617040453 -t "%1 tell: %2";};
		};
	} {
		repl $afkmessage;
		#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>%1 <100>: <170>%2<090>};
		#var thetime {@gettime{}};#line log afkcomms.txt {<100>$thetime <090>%1 <100>: <170>%2<090>}
	};
	#math currtells {$currtells + 1};
}

#action {You tell %1 '%2'$} {
	#if {$imafk == 0} {
		#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>You <170>-\> <060>%1 <090>: <160>%2<090>}
	}
}

#action {%1 says '%2'$} {#if {"%1" != "%* %*"}
	{#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>%1 : %2}};
	#if {$smscontrol == 1} {
		#system dosms -p 15617040453 -t "%1 just said %2";
	};
}

#action {%1 responds to your petition with '%2'$} {#if {"%1" != "%* %*"} {
	#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <090>%1 : <010>%2<090>};
	#system dosms -p 15617040453 -t "%1 petitioned %2";
	};}

#action {%1 GCC: '%2'$} {#if {"%1" != "%* %*"}
	{#var thetime {@gettime{}};#line log chat.txt {<100>$thetime <120>%1 : <030>%2<100>}}}

#action {%*You have learned something new about %0!} {
	#var thetime {@gettime{}};
	#line log other.txt {<100>$thetime <020>LEARNED<090> <120>: <070>%0<090>};
	#math currlrn {$currlrn + 1}
}

#action {You have learned something new about %0!} {
  #var thetime {@gettime{}};
  #line log other.txt {<100>$thetime <020>LEARNED<090> : <120>%0<090>};
	#math currlrn {$currlrn + 1}
}

#action {%1 OOC: '%2'$} {
  #if {"%1" != "%* %*"} {
    #var thetime {@gettime{}};
    #line log chat.txt {<100>$thetime <090>%1 : <120>%2<090>}
  }
}

#action {%w ASSOC: '%2'$} {
  #var thetime {@gettime{}};
  #line log chat.txt {<100>$thetime <090>%1 : <050>%2<090>}
}

#action {%w ASSOC:: '%2'$} {
  #var thetime {@gettime{}};
  #line log chat.txt {<100>$thetime <090>%1 : <050>%2<090>};
  #math curracc {$curracc + 1}
}

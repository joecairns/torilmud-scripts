#var has_globe 0;#var has_stone 0;#var has_blur 0;#var has_scale 0;#var has_displace 0;#var do_glance 1;#var nla none;#var has_mirror 0;
#var has_eleemb 0;

#action {%w is in an excellent condition. (%2)} { #var %1_hpercent 100;#var nla %1 }
#action {%w has a few scratches. (%2)} { #var %1_hpercent 95;#var nla %1 }
#action {%w has some small wounds and bruises. (%2)} { #var %1_hpercent 85;#var nla %1 }
#action {%w has quite a few wounds. (%2)} { #var %1_hpercent 65;#var nla %1 }
#action {%w has some big nasty wounds and scratches. (%2)} { #var %1_hpercent 35;#var nla %1 }
#action {%w looks pretty hurt. (%2)} { #var %1_hpercent 20;#var nla %1 }
#action {%w is in awful condition. (%2)} { #var %1_hpercent 5;#var nla %1 }

#action {%1 body seems to be made of stone!} {#var has_stone 1}
#action {%1 body is covered in dragon scales!} {#var has_scale 1}
#action {%1 form is blurred and difficult to make out!} {#var has_blur 1}
#action {%1 is consumed with elemental water!} {#var has_eleemb 1}
/* #action {%1 } {#var has_eleemb 1} */
/* #action {%1 } {#var has_eleemb 1} */
#action {%1 is surrounded by a cluster of mirror images.} {#var has_mirror 1}
#action {%1 encased in a shimmering globe!} {#var has_globe 1}
#action {%1 form is displaced, and difficult to track!} {#var has_displace 1}

#macro {\C-g} {group;#delay {1} {sg}}
#alias {group} {#send group %1;#delay {1} {sg}}

#action {<%1h/%2H %3v/%4V >} {
	#if {"$nla" == "none"} {#nop;} {
		#if {$has_scale == 1} {setaffect $nla scale} {delaffect $nla scale};
		#if {$has_stone == 1} {setaffect $nla stone} {delaffect $nla stone};
		#if {$has_mirror == 1} {setaffect $nla mirror} {delaffect $nla mirror};
		#if {$has_blur == 1} {setaffect $nla blur} {delaffect $nla blur};
		#if {$has_globe == 1} {setaffect $nla globe} {delaffect $nla globe};
		#if {$has_displace == 1} {setaffect $nla disp} {delaffect $nla disp};
	};
	#var has_stone 0;#var has_blur 0;#var has_displace 0;#var has_scale 0;#var has_mirror 0;#var has_globe 0;#var nla none;#nop;
}

#action {%1/%2 move %3/%4 hit - %0} {
        #if {"%0" != "%* %*"} {#var %0_maxhp %4;#var %0_curhp %3};
        #math {%0_hpercent} {${%0_curhp} * 100 / ${%0_maxhp}};
}

#alias {setaffect}
{
    #var %1_affects[%2] 1;
}

#alias {delaffect}
{
    #var %1_affects[%2] 0;
}

#function {affect_list} {
  #if {${%1_hpercent} >= 100} {#var result {<020>ex<100> _ <070>%1}};
  #if {${%1_hpercent} < 100 && ${%1_hpercent} >= 90} {#var result {<030>fs<100> _ <070>%1}};
  #if {${%1_hpercent} < 90 && ${%1_hpercent} >= 75} {#var result {<130>sw<100> _ <070>%1}};
  #if {${%1_hpercent} < 75 && ${%1_hpercent} >= 50} {#var result {<150>fw<100> _ <070>%1}};
  #if {${%1_hpercent} < 50 && ${%1_hpercent} >= 30} {#var result {<050>nw<100> _ <070>%1}};
  #if {${%1_hpercent} < 30 && ${%1_hpercent} >= 15} {#var result {<110>ph<100> _ <070>%1}};
  #if {${%1_hpercent} < 15 && ${%1_hpercent} >= 0} {#var result {<010>aw<100> _ <070>%1}};
  #if {${%1_affects[eleemb]}} {#var result $result <100>Emb<130>o<030>d<100>y<088>};
  #if {${%1_affects[emaint]}} {#var result $result <100>Ema<160>i<060>n<100>t<088>};
  #if {${%1_affects[scale]}} {#var result $result <130>Ds<088>};
  #if {${%1_affects[stone]}} {#var result $result <100>Ss<088>};
  #if {${%1_affects[blur]}} {#var result $result <140>Bl<088>};
  #if {${%1_affects[haste]}} {#var result $result <110>Hst<088>};
  #if {${%1_affects[fass]}} {#var result $result <010>F<110>A<088>};
  #if {${%1_affects[bark]}} {#var result $result <030>Bark<088>};
  #if {${%1_affects[mirror]}} {#var result $result <150>Mi<050>r<150>r<088>};
  #if {${%1_affects[misl]}} {#var result $result <070>M<100>i<070>sl<088>};
  #if {${%1_affects[globe]}} {#var result $result <010>Glb<088>};
  #if {${%1_affects[disp]}} {#var result $result <050>Dsp<088>};
  #if {${%1_affects[seq]}} {#var result $result <160>Seq<088>};
  #if {${%1_affects[sskin]}} {#var result $result <060>S<020>s<160>k<020>n<088>};
  #if {${%1_affects[vmind]}} {#var result $result <020>V<100>m<070>i<100>nd<088>};
  #if {${%1_affects[vit]}} {#var result $result <140>Vit<088>};
  #if {${%1_affects[pu]}} {#var result $result <100>Pu<088>};
  #if {${%1_affects[arm]}} {#var result $result <070>A<170>rm<070>o<170>r<088>};
}

#function {get_affect} {
  #unvar myspells;
	#var myspells  ;
	#if {${%1_affects[eleemb]}} {#var myspells $myspells <100>Emb<130>o<030>d<100>y<088>};
	#if {${%1_affects[emaint]}} {#var myspells $myspells <100>Ema<160>i<060>n<100>t<088>};
  #if {${%1_affects[scale]}} {#var myspells $myspells <130>Ds<088>};
  #if {${%1_affects[stone]}} {#var myspells $myspells <100>Ss<088>};
	#if {${%1_affects[mirror]}} {#var myspells $myspells <150>Mi<050>r<150>r<088>};
	#if {${%1_affects[misl]}} {#var myspells $myspells <070>M<100>i<070>sl<088>};
  #if {${%1_affects[blur]}} {#var myspells $myspells <140>Bl<088>};
  #if {${%1_affects[haste]}} {#var myspells $myspells <110>Hst<088>};
	#if {${%1_affects[fass]}} {#var myspells $myspells <010>F<110>A<088>};
  #if {${%1_affects[bark]}} {#var myspells $myspells <030>Bark<088>};
	#if {${%1_affects[seq]}} {#var myspells $myspells <160>Seq<088>};
	#if {${%1_affects[wrath]}} {#var myspells $myspells <110>W<010>rath<088>};
  #if {${%1_affects[globe]}} {#var myspells $myspells <010>Glb<088>};
  #if {${%1_affects[disp]}} {#var myspells $myspells <050>Dsp<088>};
  #if {${%1_affects[sskin]}} {#var myspells $myspells <060>S<020>s<160>k<020>n<088>};
	#if {${%1_affects[vmind]}} {#var myspells $myspells <020>V<100>m<070>i<100>nd<088>};
  #if {${%1_affects[vit]}} {#var myspells $myspells <140>Vit<088>};
  #if {${%1_affects[pu]}} {#var myspells $myspells <100>Pu<088>};
  #if {${%1_affects[arm]}} {#var myspells $myspells <070>A<170>rm<070>o<170>r<088>};
	#var result $myspells;
}

#alias {sg}
{
#list tankglanceList len tgindex;
#list rogueglanceList len rgindex;
#list otherglanceList len ogindex;

#line log spells.txt {<100> [<140>$tgindex<100>] <040>tanks};
#line log spells.txt {<100>---------------------------------------<090>};
#if {$tgindex == 0} {#nop;} {#loop {1} {&tankglanceList[]} {cnt} {#line log spells.txt { <090>@affect_list{$tankglanceList[$cnt]}}}};

#line log spells.txt {<090> };
#line log spells.txt {<100> [<110>$rgindex<100>] <010>hitters};
#line log spells.txt {<100>---------------------------------------<090>};
#if {$rgindex == 0} {#nop;} {#loop {1} {&rogueglanceList[]} {cnt} {#line log spells.txt { <090>@affect_list{$rogueglanceList[$cnt]}}}};

#line log spells.txt {<090> };
#line log spells.txt {<100> [<160>$ogindex<100>] <060>casters};
#line log spells.txt {<100>---------------------------------------<090>};
#if {$ogindex == 0} {#nop;} {#loop {1} {&otherglanceList[]} {cnt} {#line log spells.txt { <090>@affect_list{$otherglanceList[$cnt]}}}};
#line log spells.txt { };

#math totalplayers {15 - ($tgindex + $rgindex + $ogindex)};

#if {$totalplayers <= 0} {#nop;} {#loop 1 $totalplayers cnt {#line log spells.txt { <100>.<090>}}}
}

#action {You concentrate and harden your body into thick scales.} {setaffect $myname sskin;sg}
#action {Your scales become soft once more.} {delaffect $myname sskin;sg}

#action {You attack %* with monstrous fury!} {delaffect $myname fass;sg;#tick {fassup} {setaffect $myname fass;sg;#untick fassup} {90}}
#action {You assault %* with furious rage, striking %w down!} {delaffect $myname fass;sg;#tick {fassup} {setaffect $myname fass;sg;#untick fassup} {90}}

#action {%w's skin transforms into hard-plated dragon scales.} {setaffect %1 scale;sg}
#action {%w's form becomes blurred and difficult to make out!} {setaffect %1 blur;sg}
#action {%w's skin seems to turn to stone.} {setaffect %1 stone;sg}
#action {%w begins to shimmer.} {setaffect %1 globe;sg;
#tick {%1_globe30} {#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <120>-\> <090>: <070>%1's <010>GLOBE <100>expires <070>in <170>30 <100>seconds.<090>};#untick %1_globe30} {294};
#tick {%1_globe10} {#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <120>-\> <090>: <170>%1's <010>GLOBE <110>expires <070>in <170>10 <110>seconds.<090>};#untick %1_globe10} {314};
}
#action {%w starts to move with uncanny speed!} {setaffect %1 haste;sg;#tick {%1_haste} {delaffect %1 haste;#untick %1_haste;sg} {324}}
#action {%w's form is displaced.} {setaffect %1 disp;sg}
#action {%w's skin gains the texture and toughness of bark.} {setaffect %1 bark;sg}

#action {Your skin gains the texture and toughness of bark.} {setaffect $myname bark;sg}
#action {A sphere of swirling shadows envelops you...} {setaffect $myname misl;sg}
#action {You feel your skin harden to stone.} {setaffect $myname stone;sg}
#action {Your form begins to blur!} {setaffect $myname blur;sg}
#action {You begin to shimmer.} {setaffect $myname globe;sg;
#tick {my_globe30} {#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <120>-\> <090>: <070>Your <010>GLOBE <100>expires <070>in <170>30 <100>seconds.<090>};#untick my_globe30} {210};
#tick {my_globe10} {#var thetime {@gettime{}};#line log comms.txt {<100>$thetime <120>-\> <090>: <170>Your <010>GLOBE <110>expires <070>in <170>10 <110>seconds.<090>};#untick my_globe10} {230};
}
#action {Your form is displaced!} {setaffect $myname disp;sg}
#action {Several mirror images of you pop into existence at your side!} {setaffect $myname mirror;sg}
#action {You feel your skin transform into dragon scales.} {setaffect $myname scale;sg}
#action {You feel vitalized.} {setaffect $myname vit;sg}
#action {You feel your heart start to race REALLY FAST!} {setaffect $myname haste;sg}
#action {You feel cut off from the rest of the world...} {setaffect $myname seq;sg}
#action {You feel protected by powers from beyond the grave.} {setaffect $myname pu;sg}
#action {A magical energy shield envelops you!} {setaffect $myname arm;sg}
#action {Bands of magic armor wrap around you!} {setaffect $myname arm;sg}
#action {Bands of shadowy scalemail wrap around you!} {setaffect $myname arm;sg}

#action {You feel your god smile upon your destructive soul.} {setaffect $myname wrath}
#action {You gain the mindset of a ferocious viper!} {setaffect $myname vmind;sg}

#action {Your body begins to flow and liquefy as you are transformed into a water elemental!} {setaffect $myname eleemb;setaffect $myname emaint;sg}
#action {Your form begins to harden and solidify as you are transformed into an earth elemental.} {setaffect $myname eleemb;setaffect $myname emaint;sg}
#action {Your form begins to swirl and waver as you are transformed into an air elemental.} {setaffect $myname eleemb;setaffect $myname emaint;sg}
#action {Your body shimmers and sheds its elemental form, returning to normal.} {delaffect $myname eleemb;sg}
#action {Your elemental embodiment enchantment fades, and you feel able to focus again.} {delaffect $myname emaint;sg}

#alias {ccon} { #if {$condcall == 1} { gsay %0 no more !} }

#action {Your shadowy scale armor dissipates into nothingness.} {delaffect $myname arm;sg;ccon ARMOR}
#action {Your magic armor fades away.} {delaffect $myname arm;sg;ccon ARMOR}
#action {Your magical shield shudders and vanishes.} {delaffect $myname arm;sg;ccon ARMOR}
#action {Your senses dull and your blood is cooled.} {delaffect $myname vmind;sg}
#action {You feel less wooden.} {delaffect $myname bark;sg;ccon BARKSKIN}
#action {You feel less destructive.} {delaffect $myname wrath}
#action {The power of darkness fades from your body!} {delaffect $myname pu;sg}
#action {The shadows swirling around you dissipate rapidly.} {delaffect $myname misl;sg}
#action {You no longer feel cut off from the rest of the world...} {delaffect $myname seq;sg;ccon SEQUESTERED}
#action {The world speeds up around you.} {delaffect $myname haste;sg;ccon HASTED}
#action {Your dragon scales melt back into your regular flesh.} {delaffect $myname scale;sg;ccon DRAGONSCALES}
#action {Your flesh loses its stony texture.} {delaffect $myname stone;sg;ccon STONESKIN}
#action {Your form stops blurring.} {delaffect $myname blur;sg;ccon BLURRY}
#action {All of your mirror images have been destroyed!} {delaffect $myname mirror;sg}
#action {Your displaced form snaps back to your physical location!} {delaffect $myname disp;sg;ccon DISPLACED}
#action {Your image is no longer displaced.} {delaffect $myname disp;sg;ccon DISPLACED}
#action {Your globe shimmers, and fades into the air.} {delaffect $myname globe;sg;ccon GLOBED}
#action {Your vitality drains away.} {delaffect $myname vit;sg;ccon VITALIZED}

#alias sadd {#list tocastlist ins -1 %0;#show <100>...<090> Added <130>%0 <090>to cast queue}
#alias sadd2 {#list tocastlist ins 1 %0;#show <100>...<090> Added <130>%0 <090>to cast queue}
#alias sdel {#list tocastlist fnd %0 tocastdelidx;#list tocastlist del {$tocastdelidx}}

#var tocastidx 1
#var spell_run 0

#alias srun {
	#var spell_run 1;
	#list tocastlist len tcindex;
	#list tocastlist get $tocastidx nextspell;
	$nextspell
}

#alias sut {
	#list tocastlist clear;
	#if {$spell_run == 1} {#nop;} {
	#loop {1} {&tankglanceList[]} {cnt} {
		#if {"%1" == "g"} {
		#if {${$tankglanceList[$cnt]_affects[globe]} == 0} {sadd globe $tankglanceList[$cnt]};
		} {
		#if {${$tankglanceList[$cnt]_affects[blur]} == 0} {sadd2 bl $tankglanceList[$cnt]};
                #if {${$tankglanceList[$cnt]_affects[scale]} == 0} {sadd ds $tankglanceList[$cnt]};
		}
	};
	srun;
	}
}

#macro {\C-s} {sut}

#alias su {
	#list tocastlist clear;
	#if {$spell_run == 1} {#nop;} {
	#if {${@upfirst{%1}_affects[blur]} == 0} {sadd bl %1};
	#if {${@upfirst{%1}_affects[stone]} == 0} {sadd ss %1};
	#if {${@upfirst{%1}_affects[haste]} == 0} {sadd hst %1};
	#if {${@upfirst{%1}_affects[globe]} == 0} {sadd globe %1};
	srun;
	}
}

#showme __ SPELLTRACKER.tin

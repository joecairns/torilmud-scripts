#var travel 0

#function fixnew
{
     #if {"%1" == "-N"}  {#var result north};
     #if {"%1" == "-E"}  {#var result east};
     #if {"%1" == "-S"}  {#var result south};
     #if {"%1" == "-W"}  {#var result west};
     #if {"%1" == "-U"}  {#var result up};
     #if {"%1" == "-D"}  {#var result down}
}

#function revdir
{
     #if {"%1" == "north"} {#var result -S};
     #if {"%1" == "east"}  {#var result -W};
     #if {"%1" == "south"} {#var result -N};
     #if {"%1" == "west"}  {#var result -E};
     #if {"%1" == "up"}    {#var result -D};
     #if {"%1" == "down"}  {#var result -U}
}

#function longdir
{
     #if {"%1" == "n"} {#var result north};
     #if {"%1" == "e"} {#var result east};
     #if {"%1" == "s"} {#var result south};
     #if {"%1" == "w"} {#var result west};
     #if {"%1" == "u"} {#var result up};
     #if {"%1" == "d"} {#var result down}
}

#function shortdir
{
     #if {"%1" == "north"} {#var result n};
     #if {"%1" == "east"}  {#var result e};
     #if {"%1" == "south"} {#var result s};
     #if {"%1" == "west"}  {#var result w};
     #if {"%1" == "up"}    {#var result u};
     #if {"%1" == "down"}  {#var result d}
}

#function finddir
{
     #if {"%1" == "%2"}
     {
          #var result %3
     }
     {
          #var result %2
     }
}

#alias go {
     #if {$travel}
     {
          #var travel 0;
          #showme <100>[_ <070>You <010>stop <110>traveling<100>. _]
     }
     {
          #var travel 1;
          #var lastdir @longdir{%1};
          #var lastdir @revdir{$lastdir};
          #showme <100>.. <170>T<070>raveling <140>%1<100>;
          %0
     }
}

#action {Exits: %0}
{
     #if {$travel}
     {
          #if {"%0" != "%* %*"}
          {
               #var travel 0;
               #showme <100>[_ <110>D<010>ead <070>End <100>_]
          };

          #if {"%0" == "%* %* %*"}
          {
               #var travel 0;
               #showme <100>[_ <170>C<070>rossroads <120>R<020>eached <100>_]
          };

          #if {$travel}
          {
               #var newdir @finddir{$lastdir %0};
               #var newdir @fixnew{$newdir};
	       #showme <100>.. <170>T<070>raveling <140>$newdir<100>;

               #var lastdir @revdir{$newdir};

               @shortdir{$newdir}
          }
     };
}

#action {You need to board a ship to travel the oceans.} {
	#if {$travel} {go}}

#action {Alas, you cannot go that way} {#if {$travel} {go}}

#showme __ TRAVEL.tin
